-- Funk language grammar.

comment "//" ;
comment "/*" "*/" ;

Program.	Prog ::= [Decl] "main" Exp ;

Declaration.	Decl ::= Sign [Sign] "be" Exp ;

Signature.	Sign ::= Type ":" Ident;

ELet.		Exp ::= "let" Decl "in" Exp ;
ECase.		Exp ::= "case" Exp "of" [Patt] ;
EIf.		Exp ::= "if" Exp "then" Exp "else" Exp "fi";
ELambda.	Exp ::= Type ":lambda" [Sign] "->" Exp ;
EGet.		Exp1 ::= Ident ;
EApplication.	Exp1 ::= "(" Ident [Exp1] ")" ;
EList.		Exp1 ::= "[" [Exp2] "]" ;
EEmptyList.	Exp1 ::= "[]" ;
EEmptyTuple.	Exp1 ::= "{}" ;
ETuple.		Exp1 ::= "{" [Exp2] "}" ; 
EInt.		Exp1 ::= Integer ;
EFalse.		Exp1 ::= "False" ;
ETrue.		Exp1 ::= "True" ;
EPom.		Exp2 ::= Exp1 ;

coercions Exp 1 ;

Pattern.	Patt ::= Match "->" Exp;

MatchExp.	Match ::= Exp ;
MatchHeadTail.	Match ::= Ident "::" Ident ;
MatchTuple.	Match ::= "{" [Ident] "}" ;

TypeFun.	Type ::= Type1 "=>" Type;
TypeTuple.	Type1 ::= "{" [Type] "}" ;
TypeList.	Type1 ::= "[" Type "]" ;
TypeInt. 	Type1 ::= "int";
TypeBool.	Type1 ::= "bool";
coercions Type 1;

separator Ident "," ;
separator Decl ";" ;
separator Exp "" ;
separator Exp1 "" ;
separator Exp2 "," ;
separator Patt "|" ;
separator Sign "";
separator Type ".";
